apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        consumerProguardFiles "consumer-rules.pro"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    testImplementation 'junit:junit:4.12'
    testImplementation "io.mockk:mockk:1.10.0"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                group = 'com.github.rmielnik'
                artifactId = 'list'
                version = '1.0.0'

                // Adds javadocs and sources as separate jars.
                artifact androidJavadocsJar
                artifact(sourceJar)

                pom {
                    name = 'SimplerRecyclerView'
                    description = 'A tiny abstraction that speeds up working with RecyclerView.'
                    url = 'https://github.com/rmielnik/SimplerRecyclerView'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'rmielnik'
                            name = 'Robert Mielnik'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:rmielnik/SimplerRecyclerView.git'
                        developerConnection = 'scm:git@github.com:rmielnik/SimplerRecyclerView.git'
                        url = 'https://github.com/rmielnik/SimplerRecyclerView'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

tasks.withType(Javadoc).all {
    enabled = false
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$rootDir/documentation"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompiler.classpath
        }
    }
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}